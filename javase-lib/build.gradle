plugins {
    id 'java-library'
    id 'maven-publish'
}

def clientDepVersion = project.'lsJavaSE.depVersion'

dependencies {
    rootProject.ext.commonLibs.each { implementation it }
    implementation "com.lightstreamer:ls-javase-client:$clientDepVersion"
}

sourceSets {
    main {
        java.srcDirs = ['../src/main/java','../src/java-se/java']
        resources.srcDir '../src/java-se/resources'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    title "$POM_JAVASE_LIBRARY_NAME $version API Specification"
    source = sourceSets.main.allJava
    exclude "**/*DisconnectionHandler*"
    exclude "**/descriptors"
    exclude "**/utils"
    options.overview '../src/java-se/javadoc/overview.html'
    options.links "${project.'lsJavaSE.javadocsLink'}/$clientDepVersion"
    options.links project.'jms.javadocsLink'
    options.addBooleanOption('Xdoclint', true)
}

publishing {
    publications {
        javaSElibrary(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = POM_JAVASE_LIBRARY_NAME
                description = POM_DESCRIPTION
                url = POM_URL
                licenses {
                    license {
                        name = POM_LICENSE_NAME
                        url = POM_LICENSE_URL
                    }
                }
                developers {
                    developer {
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                        organization = POM_DEVELOPER_ORGANIZATION
                        organizationUrl = POM_DEVELOPER_ORGANIZATION_URL
                    }
                }
                scm {
                    connection = "scm:git:${POM_URL}.git"
                    developerConnection = "scm:git:${POM_URL}.git"
                    url = POM_URL
                }
            }
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = "file://${buildDir}/repo"
        }
    }
}

check.dependsOn(rootProject.tasks.verifyGoogleJavaFormat)